# Maintainer: Manthan M. Patil <manthanabc@example.com>
pkgname=polly-git
pkgver=r1.0.1.0
pkgrel=1
pkgdesc="A Polkit authentication agent for Wayland desktops (git version)"
arch=('x86_64' 'aarch64')
url="https://github.com/manthanabc/polkit-agent"
license=('MIT')
depends=('glib2' 'polkit')
makedepends=('rust' 'cargo' 'git')
provides=('polly')
conflicts=('polly')
install=polly.install
source=("git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/polkit-agent"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/polkit-agent"
  cargo build --release --locked
}

check() {
  cd "$srcdir/polkit-agent"
  cargo test --release --locked
}

package() {
  cd "$srcdir/polkit-agent"
  install -Dm755 "target/release/polkit-agent" "$pkgdir/usr/bin/polly"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 Readme.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm644 polly.service "$pkgdir/usr/lib/systemd/user/polly.service"
}
