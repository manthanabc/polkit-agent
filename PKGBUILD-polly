# Maintainer: Manthan M. Patil <manthanabc@example.com>
pkgname=polly
pkgver=0.1.0
pkgrel=1
pkgdesc="A Polkit authentication agent for Wayland desktops"
arch=('x86_64' 'aarch64')
url="https://github.com/manthanabc/polkit-agent"
license=('MIT')
depends=('glib2' 'polkit')
makedepends=('rust' 'cargo')
install=polly.install
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('SKIP')

build() {
  cd "$srcdir/polkit-agent-$pkgver"
  cargo build --release --locked
}

check() {
  cd "$srcdir/polkit-agent-$pkgver"
  cargo test --release --locked
}

package() {
  cd "$srcdir/polkit-agent-$pkgver"
  install -Dm755 "target/release/polkit-agent" "$pkgdir/usr/bin/polly"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 Readme.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm644 polly.service "$pkgdir/usr/lib/systemd/user/polly.service"
}
